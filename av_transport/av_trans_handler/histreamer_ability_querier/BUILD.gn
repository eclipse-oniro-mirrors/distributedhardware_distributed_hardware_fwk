# Copyright (c) 2023-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import(
    "//foundation/distributedhardware/distributed_hardware_fwk/av_transport/distributed_av_transport.gni")

config("handler_external_config") {
  include_dirs = [
    "${common_path}/include",
    "${engine_path}/av_sender/include",
    "${engine_path}/av_receiver/include",
    "${interface_path}",
  ]
}

ohos_shared_library("histreamer_ability_querier") {
  sanitize = {
    boundary_sanitize = true
    integer_overflow = true
    ubsan = true
  }
  branch_protector_ret = "pac_ret"
  public_configs = [ ":handler_external_config" ]

  include_dirs = [
    "include",
    "${common_path}/include",
  ]

  sources = [
    "${common_path}/src/av_trans_log.cpp",
    "src/histreamer_ability_querier.cpp",
  ]

  defines = [
    "HI_LOG_ENABLE",
    "DH_LOG_TAG=\"av_trans_handler\"",
    "LOG_DOMAIN=0xD004101",
    "HST_ANY_WITH_NO_RTTI",
  ]

  if (histreamer_compile_part) {
    external_deps = [
      "media_foundation:histreamer_base",
      "media_foundation:histreamer_codec_filters",
      "media_foundation:histreamer_ffmpeg_convert",
      "media_foundation:histreamer_plugin_base",
    ]
  }

  external_deps += [
    "bounds_checking_function:libsec_shared",
    "cJSON:cjson",
    "hilog:libhilog",
  ]

  cflags = [
    "-O2",
    "-fPIC",
    "-Wall",
    "-fexceptions",
    "-Wno-unused-but-set-variable",
    "-Wno-format",
    "-fstack-protector-strong",
  ]

  cflags_cc = cflags

  ldflags = [
    "-fpie",
    "-Wl,-z,relro",
    "-Wl,-z,now",
  ]

  remove_configs = [
    "//build/config/compiler:no_rtti",
    "//build/config/compiler:no_exceptions",
  ]

  part_name = "distributed_hardware_fwk"
  subsystem_name = "distributedhardware"
}

ohos_rust_shared_ffi("histreamer_ability_querier_core") {
  sources = [ "src/histreamer_ability_querier_core.rs" ]
  crate_name = "histreamer_ability_querier_core"
  crate_type = "cdylib"
  subsystem_name = "distributedhardware"
  part_name = "distributed_hardware_fwk"
}
